apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace "su.linka.linkapaperboard"
    compileSdk 34
    defaultConfig {
        applicationId "su.linka.linkapaperboard"
        minSdk 19
        targetSdk 34
        versionCode 6
        versionName "1.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    def keystorePath = System.getenv("ANDROID_KEYSTORE")
    def hasReleaseSigning = keystorePath != null && file(keystorePath).exists()
    signingConfigs {
        if (hasReleaseSigning) {
            release {
                storeFile file(keystorePath)
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.robolectric:robolectric:4.12.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // AppMetrica from maven central.
    implementation 'com.yandex.android:mobmetricalib:3.2.1'
    // Optional. Play Install Referrer library for better attribution.
    implementation 'com.android.installreferrer:installreferrer:2.2'
}
